/*
 * Przemyslaw Malirz
 *
 * DLock JDBC - Distributed Lock
 */
buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'me.champeau.gradle.jmh' version '0.4.8'
}

apply plugin: "kotlin"
apply plugin: "groovy"

// Enable Oracle tests (for unit tests and jmh)
if (project.hasProperty("oracleUser")) {
    println "Enabling Oracle test and jmh dependencies for $oracleUser/*******"
    apply from: 'oracle-build.gradle'
}

dependencies {
    compile "com.dlock:dlock-core:1.0-SNAPSHOT"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile "org.codehaus.groovy:groovy-all:2.5.6"
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
    testCompile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testRuntime "net.bytebuddy:byte-buddy:1.8.21"
    testRuntime "org.objenesis:objenesis:2.6"

    jmh group: 'com.h2database', name: 'h2', version: '1.4.197'
    jmh group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.dlock'
            version = '1.0-SNAPSHOT'
            from components.java
        }
    }
}

jacoco {
    toolVersion = "0.8.4"
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacoco-report")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }

        rule {
            enabled = false
            element = 'CLASS'
            includes = ['com.dlock.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

jmh {
    fork = 2
    iterations = 2
    jmhVersion = '1.21'
    profilers = ['gc']
    threads = 5
    warmup = '1s'
    warmupBatchSize = 1
    warmupIterations = 1
    jvmArgs = ['-Xms512m', '-Xmx1024m', '-Djmh.separateClasspathJAR=true', '-Dlog4j.configurationFile=conf/log4j2.xml']
}

jmhJar {
    exclude 'log4j2.xml'
}
