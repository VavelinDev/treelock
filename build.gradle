/*
 * Przemyslaw Malirz
 *
 * DLock - Distributed Lock
 */
buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'me.champeau.gradle.jmh' version '0.4.8'
}

apply plugin: "kotlin"
apply plugin: "groovy"

dependencies {
    // Use JUnit test framework
    testCompile "org.codehaus.groovy:groovy-all:2.5.6"
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "net.bytebuddy:byte-buddy:1.8.21"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.6"    // allows mocking of classes without default constructor (together with CGLIB)
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    jmh group: 'com.h2database', name: 'h2', version: '1.4.197'
    jmh group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.bbh.dlock'
            version = '1.0'
            from components.java
        }
    }
}

jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("$buildDir/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacoco-report")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }

        rule {
            enabled = false
            element = 'CLASS'
            includes = ['com.malirz.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

jmh {
    fork = 2
    iterations = 2
    jmhVersion = '1.21'
    profilers = ['gc']
    threads = 5
    warmup = '1s'
    warmupBatchSize = 1
    warmupIterations = 1
    jvmArgs = ['-Xms512m', '-Xmx512m', '-Djmh.separateClasspathJAR=true']
}

jmhJar {
    exclude 'log4j2.xml'
}

