plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id 'jacoco'
    id 'net.researchgate.release' version '3.0.2' apply false
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5" apply false
}

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects {

    apply plugin: "kotlin"
    apply plugin: "groovy"
    apply plugin: "jacoco"
    apply plugin: 'maven-publish'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'com.jfrog.bintray'
    apply from: '../build-bintray.gradle'

    project.group = 'com.dlock'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        testImplementation "org.apache.groovy:groovy-all:4.0.17"
        testImplementation "org.spockframework:spock-core:2.4-M1-groovy-4.0"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
        testImplementation "net.bytebuddy:byte-buddy:1.14.11"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    jacoco {
        toolVersion = "0.8.11"
        reportsDirectory = file("$buildDir/jacoco")
    }

    jacocoTestReport {
        reports {
            html.destination = (file("$buildDir/jacoco-report"))
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                includes = ["$project.group.*"]
            }

            rule {
                element = 'METHOD'
                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 25
                }
                includes = ["$project.group.*"]
            }

            rule {
                element = 'METHOD'
                limit {
                    counter = 'BRANCH'
                    value = 'TOTALCOUNT'
                    maximum = 4
                }
                includes = ["$project.group.*"]
            }

        }
    }

    jar {
        manifest {
            attributes(
                    "Name": "${project.group} - ${project.name}",
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Implementation-Vendor": "Przemyslaw Malirz")
        }
    }

    afterReleaseBuild.dependsOn bintrayUpload

}

tasks.register('codeCoverageReport', JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.required = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
        html.required = true
        csv.required = false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

