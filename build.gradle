/*
 * Przemyslaw Malirz
 *
 * dlock.
 */
plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'maven'
    id 'jacoco'
    id 'net.researchgate.release' version '2.6.0' apply false
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5" apply false
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

subprojects {

    apply plugin: "kotlin"
    apply plugin: "groovy"
    apply plugin: "jacoco"
    apply plugin: 'maven-publish'
    apply plugin: 'net.researchgate.release'
    apply plugin: 'com.jfrog.bintray'
    apply from: '../build-bintray.gradle'

    project.group = 'com.dlock'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        testImplementation "org.codehaus.groovy:groovy-all:2.5.8"
        testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    jacoco {
        toolVersion = "0.8.5"
        reportsDir = file("$buildDir/jacoco")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("$buildDir/jacoco-report")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                includes = ["$project.group.*"]
            }

            rule {
                element = 'METHOD'
                limit {
                    counter = 'LINE'
                    value = 'TOTALCOUNT'
                    maximum = 25
                }
                includes = ["$project.group.*"]
            }

            rule {
                element = 'METHOD'
                limit {
                    counter = 'BRANCH'
                    value = 'TOTALCOUNT'
                    maximum = 4
                }
                includes = ["$project.group.*"]
            }

        }
    }

    jar {
        manifest {
            attributes(
                    "Name": "${project.group} - ${project.name}",
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Implementation-Vendor": "Przemyslaw Malirz")
        }
    }

    afterReleaseBuild.dependsOn bintrayUpload

}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

